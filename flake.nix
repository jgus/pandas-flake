{
  description = "pandas: version-bumped independently from nixpkgs through a Python package overlay.";

  inputs = {
    nixpkgs.url = "github:NixOS/nixpkgs/nixpkgs-unstable";
    flake-utils.url = "github:numtide/flake-utils";
    flake-lib = {
      url = "github:jgus/flake-lib/v1";
      inputs.nixpkgs.follows = "nixpkgs";
      inputs.flake-utils.follows = "flake-utils";
    };
  };

  outputs = { nixpkgs, flake-utils, flake-lib, ... }:
    let
      pin = import ./pin.nix;
      inherit (pin) version hash;
      source = { type = "pypi"; pname = "pandas"; format = "sdist"; };
      overlay = final: prev: {
        pythonPackagesExtensions = prev.pythonPackagesExtensions ++ [
          (pyfinal: pyprev: {
            pandas = pyprev.pandas.overridePythonAttrs (_: {
              inherit version;
              doCheck = false;
              postPatch = ''
                substituteInPlace pyproject.toml \
                  --replace-fail "numpy>=2.0" numpy
              '';
              src = pyfinal.fetchPypi { inherit version hash; pname = "pandas"; };
            });
          })
        ];
      };
    in
    flake-utils.lib.eachDefaultSystem
      (system:
        let
          pkgs = import nixpkgs {
            inherit system;
            overlays = [ overlay ];
          };
        in
        {
          packages = {
            pandas = pkgs.python3.pkgs.pandas;
            default = pkgs.python3.pkgs.pandas;
            update-version = flake-lib.lib.mkUpdateVersion { inherit pkgs source; buildAttr = "pandas"; };
            update-branches = flake-lib.lib.mkUpdateBranches { inherit pkgs source; pinSchema = "pypi"; };
          };
        }) // {
      overlays.default = overlay;
    };
}
